buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.6"
        classpath "org.grails.plugins:hibernate5:7.3.0"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.7"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.3.2"
    id "org.flywaydb.flyway" version "5.2.4"
}

version "2.1.0-SNAPSHOT"

group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"org.grails.grails-gsp"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin: 'maven-publish'


//apply from: 'https://raw.githubusercontent.com/AtlasOfLivingAustralia/travis-build-configuration/master/travis_grails_publish.gradle'

sourceCompatibility = 1.11
targetCompatibility = 1.11


//grails {
//    exploded = true
//    plugins {
//        compile project(':images-client-plugin')
//    }
//}

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    implementation group: 'org.grails', name: 'grails-async-gpars'

    implementation group: 'org.grails.plugins', name: 'grails-pretty-time', version: '4.0.0'
    implementation 'org.grails.plugins:csv:1.0.1'

    implementation 'xml-apis:xml-apis:1.4.01' // TODO do we need these??

    implementation 'com.opencsv:opencsv:5.7.1'
    implementation group: 'com.googlecode.owasp-java-html-sanitizer', name: 'owasp-java-html-sanitizer', version: '20211018.1'

    // ALA plugins
    implementation group: 'org.grails.plugins', name: 'ala-auth', version: alaSecurityLibsVersion, changing: true
    implementation group: 'org.grails.plugins', name: 'ala-ws-plugin', version: alaSecurityLibsVersion, changing: true
    implementation group: 'org.grails.plugins', name: 'ala-ws-security-plugin', version: alaSecurityLibsVersion, changing: true
    implementation group: 'org.grails.plugins', name: 'ala-bootstrap3', version: '4.1.0', changing: true
    implementation group: 'org.grails.plugins', name: 'ala-admin-plugin', version: '2.3.0', changing: true
    implementation group: 'au.org.ala.plugins.grails', name:'images-client-plugin', version: '1.1', changing: true

    // Added dependencies
    runtimeOnly 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.grails.plugins:postgresql-extensions:7.0.0'
    implementation "org.flywaydb:flyway-core:5.2.4"
    implementation 'org.grails.plugins:cache-headers:2.0.2'
    runtimeOnly 'org.codehaus.groovy:groovy-dateutil'
    implementation 'dk.glasius:external-config:3.1.1'

    implementation 'org.grails.plugins:quartz:2.0.13'
    implementation 'org.quartz-scheduler:quartz:2.2.1' // Is not pulled in by default

    implementation group: 'org.locationtech.spatial4j', name: 'spatial4j', version: '0.7'
    implementation group: 'org.locationtech.jts', name: 'jts-core', version: '1.15.0'
    implementation "com.amazonaws:aws-java-sdk-s3:$amazonAwsSdkVersion"
    implementation 'org.javaswift:joss:0.10.4'
    runtimeOnly 'org.postgresql:postgresql:42.3.8'

    implementation 'org.elasticsearch:elasticsearch:7.17.1'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.17.1'
    // override log4j in elasticsearch plugin
    // TODO remove this once no log4j vulnerable transitive deps are used.
//    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
//    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'

    implementation 'org.imgscalr:imgscalr-lib:4.2'
    implementation 'org.apache.commons:commons-imaging:1.0-SNAPSHOT'
    implementation 'org.apache.tika:tika-core:2.5.0'
    implementation 'com.github.jai-imageio:jai-imageio-core:1.4.0'
    implementation 'au.org.ala:image-utils:1.9.0'

    implementation 'net.lingala.zip4j:zip4j:2.10.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.apache.avro:avro:1.11.0'
    implementation 'org.xerial.snappy:snappy-java:1.1.8.4'

    // Swagger
    implementation 'au.org.ala.plugins:openapi:1.1.0'

    // Google analytics requirement
    implementation 'com.google.api-client:google-api-client:1.33.2'
    implementation 'com.google.apis:google-api-services-analytics:v3-rev20190807-1.32.1'

    // Standard grails
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-core:5.6.11.Final"
    implementation "org.grails.plugins:events"
    implementation "org.grails.plugins:gsp"
    profile "org.grails.profiles:web"
    runtimeOnly "org.glassfish.web:el-impl:2.2.1-b05"
//    runtimeOnly "com.h2database:h2"
//    runtimeOnly "org.apache.tomcat:tomcat-jdbc"
    runtimeOnly "javax.xml.bind:jaxb-api:2.3.1"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.4.7"
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:4.0.0"
    testImplementation "org.seleniumhq.selenium:selenium-api:4.0.0"
    testImplementation "org.seleniumhq.selenium:selenium-support:4.0.0"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-chrome-driver:4.0.0"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-firefox-driver:4.0.0"

    implementation "org.grails.plugins:cache-ehcache:3.0.0"
    implementation "org.hibernate:hibernate-jcache"
    runtimeOnly 'org.ehcache:ehcache'

//    testCompile 'io.micronaut:micronaut-http-client'

    // Testing
    testImplementation 'ru.vyarus:spock-junit5:1.0.1'
    testImplementation 'org.grails.plugins:embedded-postgres:1.1.2'
    testImplementation "com.opentable.components:otj-pg-embedded:0.13.0" // required transitive dependency from the plugin.
    testImplementation 'cloud.localstack:localstack-utils:0.2.22'
    testImplementation "com.amazonaws:aws-java-sdk:$amazonAwsSdkVersion" // full AWS SDK included in test scope for localstack config
    testImplementation 'com.palantir.docker.compose:docker-compose-junit-jupiter:1.8.0'
}

bootJar {
    enabled = true
    classifier = 'exec'
    launchScript()
}

bootWar {
    enabled = false
}

springBoot {
    buildInfo()
//    layout = 'WAR'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

webdriverBinaries {
    if (!System.getenv().containsKey('GITHUB_ACTIONS')) {
        chromedriver {
            version = '2.45.0'
            fallbackTo32Bit = true
        }
        geckodriver '0.30.0'
    }
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    if (!System.getenv().containsKey('GITHUB_ACTIONS')) {
        systemProperty 'webdriver.chrome.driver', System.getProperty('webdriver.chrome.driver')
        systemProperty 'webdriver.gecko.driver', System.getProperty('webdriver.gecko.driver')
    } else {
        systemProperty 'webdriver.chrome.driver', "${System.getenv('CHROMEWEBDRIVER')}/chromedriver"
        systemProperty 'webdriver.gecko.driver', "${System.getenv('GECKOWEBDRIVER')}/geckodriver"
    }
}

assets {
    minifyJs = true
    minifyCss = true
    includes = ["fonts/*"]
}

publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            artifact bootJar
        }
    }
}
